
plugins {
    id 'application'
	id 'java'
	id "org.panteleyev.jpackageplugin" version "1.6.0"
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "io.github.spair:imgui-java-app:1.86.11"
    
    //Google libraries
    implementation 'com.google.guava:guava:30.1.1-jre'
	//GSON
    implementation 'com.google.code.gson:gson:2.9.1'
	
	//Netty
	implementation('io.netty:netty-all:4.1.75.Final') {
        exclude group: 'io.netty', module: 'netty-tcnative'
    }
    
    //exp4j
    implementation 'net.objecthunter:exp4j:0.4.8'
    
    //Apache commons
    implementation 'org.apache.commons:commons-collections4:4.5.0-M1'
    
    //STB image
	implementation "org.lwjgl:lwjgl-stb"
	runtimeOnly "org.lwjgl:lwjgl-stb::natives-windows"
	
	//Socket.IO
	implementation "io.socket:socket.io-client:1.0.1"
}

application {
	mainClass.set("ingage.IngageClient")
}

task copyDependencies(type: Copy) {
	from(configurations.runtimeClasspath).into("$buildDir/jars")
}

task copyJar(type: Copy) {
    from(tasks.jar).into("$buildDir/jars")
}

jar {
	archiveFileName = 'Ingage Client.jar'
	manifest {
		attributes 'Main-Class': 'ingage.IngageClient'
	}
}

tasks.compileJava {
	dependsOn clean
}

tasks.jpackage {
    dependsOn build, copyDependencies, copyJar

    input  = "$buildDir/jars"
    destination = "$buildDir/dist"

    appName = "Ingage Client"
	appVersion = '1.0.0'

    mainJar = tasks.jar.archiveFileName.get()
    mainClass = "ingage.IngageClient"

    windows {
		winDirChooser = true
		winShortcutPrompt = true
		winMenu = true
		winShortcut = true
		icon = "icons.ico"
    }
}

task renameInstaller(type: Copy) {
	from layout.buildDirectory.dir("dist")
    into layout.buildDirectory.dir("dist")
    rename tasks.jpackage.appName+"-"+tasks.jpackage.appVersion+".exe", tasks.jpackage.appName+" Installer-v"+tasks.jpackage.appVersion+".exe"
}

task deleteOldInstaller(type: Delete) {
	dependsOn renameInstaller
	delete layout.buildDirectory.file("dist/"+tasks.jpackage.appName+"-"+tasks.jpackage.appVersion+".exe")
}

jpackage.finalizedBy deleteOldInstaller
